#!/bin/sh
# shellcheck shell=dash
# Per-device byte counters from conntrack (for speed calculation).
# Output: JSON array [{"ip":"...","bytes_in":N,"bytes_out":N}]

. /usr/local/bin/trafficctl-fw.sh

# Any offload mode (software, hardware, hardware-counter) bypasses conntrack counters
# for fast-path packets. Use nftables counters at forward priority -200 (before the
# flowtable at -150) which capture every packet regardless of offload state.
# Only pure "none" mode has accurate conntrack counters.
_offload=$(tctl_get_offload_mode)
[ "$_offload" != "none" ] && [ "$TCTL_FW" = "nft" ] && exec /usr/local/bin/trafficctl-bytes-nft.sh

# All LAN subnets (multi-bridge / multi-VLAN aware), as awk membership spec.
MATCH_SPEC=$(tctl_lan_subnets | awk '{printf "%s%s:%s:%s",(NR>1?" ":""),$2,$3,$4}')
[ -z "$MATCH_SPEC" ] && { echo '[]'; exit 0; }

cat /proc/net/nf_conntrack 2>/dev/null | awk -v spec="$MATCH_SPEC" '
function ip2int(ip,   a) {
    split(ip, a, ".")
    return a[1]*16777216 + a[2]*65536 + a[3]*256 + a[4]
}
function is_lan(ip,   si, k) {
    si = ip2int(ip)
    for (k = 1; k <= ns; k++)
        if (si - (si % blk[k]) == base[k]) return 1
    return 0
}
BEGIN {
    printf "["
    ns = split(spec, parts, " ")
    for (k = 1; k <= ns; k++) {
        split(parts[k], kv, ":")
        base[k] = kv[1] + 0; blk[k] = kv[2] + 0
    }
}
{
    src=""; bytes_orig=0; bytes_reply=0; bc=0
    for (i=1; i<=NF; i++) {
        if ($i ~ /^src=/) {
            v = substr($i, 5)
            if (src == "" && is_lan(v)) src = v
        }
        if ($i ~ /^bytes=/) {
            v = substr($i, 7) + 0
            bc++
            if (src != "" && bc == 1) bytes_orig = v
            else if (src != "" && bc == 2) bytes_reply = v
        }
    }
    if (src != "") {
        key = src
        in_total[key] += bytes_reply
        out_total[key] += bytes_orig
    }
}
END {
    n = 0
    for (ip in in_total) {
        if (n > 0) printf ","
        printf "{\"ip\":\"%s\",\"bytes_in\":%d,\"bytes_out\":%d}", ip, in_total[ip], out_total[ip]
        n++
    }
    printf "]\n"
}
'
